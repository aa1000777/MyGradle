// 声明是Android程序
apply plugin: 'com.android.application'

android {
    //设置签名
    signingConfigs {
        shldKey {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
    }

    // 编译SDK的版本
    compileSdkVersion 25
    // build tools的版本
    buildToolsVersion "25.0.2"
    defaultConfig {
        // 应用的包名
        applicationId "com.cjc.mygradle"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField("String", "API_HOST", API_HOST_DEBUG)
            signingConfig signingConfigs.shldKey
        }
        debugTest {
            debuggable true
            minifyEnabled false
            buildConfigField("String", "API_HOST", API_HOST_TEST)
            signingConfig signingConfigs.shldKey
        }
        release {
            //minifyEnabled主要用来控制是否运行混淆的
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "API_HOST", API_HOST_RELEASE)
            signingConfig signingConfigs.shldKey
        }
    }

    //打包名称修改
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def fileName = "MyGradle-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}-${variant.buildType.name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

//获取系统时间
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    testCompile 'junit:junit:4.12'
}


